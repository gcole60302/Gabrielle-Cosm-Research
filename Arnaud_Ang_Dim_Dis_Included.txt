;------------------------------------------------------------------------------
; NAME:
;       ARNAUD
;
; PURPOSE:
;       Calculate the Arnaud profile for a cluster of a given mass and redshift.
;
; EXAMPLE SYNTAX:
;       arnaud, m500, redshift, r_arcmin, dt_uK
;
; INPUTS:
;       - M500: the cluster mass in Msun
;       - redshift: the cluster redshift
;
; OUTPUTS:
;       - r_arcmin: the radial coordinate in arcminutes
;       - dT_uK: the array of temperature decrements (in uK) corresponding to r_arcmin
;
; DEFAULTS:
;       - H0 = 72 km/s/Mpc
;       - OmegaM = 0.27
;       - cutoff = 6 (cuts off NFW profile at this fraction of R500)
;
; AUTHOR:  TdH
;
; DATE CREATED: 9May2012
;
; TO DO:
;       - Promote hardcoded defaults to arguments
;
; NOTES:
;       - This is an IDL port of LDS's project_arnaud_profile.m
;
; REVISION HISTORY:
;
;------------------------------------------------------------------------------

FUNCTION EFACT, z, omm
RETURN, sqrt( omm*(1d + z)^3 + (1d - omm))
END

FUNCTION EFACT_D, z
RETURN, (1d)/(sqrt( 0.27d*(1d + z)^3 + (1d - 0.27d)));put this in in order to perform the intergration needed for Ang_Diam_Dist
END

FUNCTION RHO_CRIT, z, H0, omm
G = 4.30117902d-9 ; in km^2 Mpc / s^2 Msun such that rho_crit is in Msun/Mpc^3
RETURN, 3d*H0^2 / (8d * !dpi * G) * Efact(z, omm)^2
END

FUNCTION PROJ_ARNAUD_PROFILE, x
COMMON cblock, y, params
alpha = params[0]
beta = params[1]
gamma = params[2]
c500 = params[3]
R500 = params[4]
index = (beta-gamma)/alpha
x1 = x * c500/R500
y1 = y * c500/R500
proj = 1d / ((x1^2 + y1^2)^(gamma/2d)*(1d + (x1^2 + y1^2)^(alpha/2d))^index)
RETURN, proj
END

FUNCTION ANG_DIAM_DIST, H0, z
ang_dist = ((3e8)/(H0))*(QSIMP('EFACT_D', 0, z))/ (1+ z) ;assumes flat cosmology
RETURN, ang_dist
END


PRO ARNAUD, m500, redshift, r_arcmin, dT_uK, stopit=stopit
COMMON cblock, y, params
; defaults
if n_elements(m500) eq 0 then m500 = 6e14 ; Msun
if n_elements(redshift) eq 0 then redshift = 0.4
if n_elements(stopit) eq 0 then stopit = 0
cutoff = 6. ; cutoff in units of R500
H0 = 72. ; km/s/Mpc
OmegaM = 0.27
hubble = H0/100.
hubble70 = H0/70.
mmw = 0.59
mu_e = 1.143
m_p  = 1.6726e-27 ; proton mass in kg
mpc = 3.08568025e22 ;  in m
Msol = 1.98892e30 ; in kg
sigma_T = 6.652e-25/(100.^2) ; now in m^2
m_e = 9.11e-31 ; electron mass in kg
k_b = 1.3806503e-23 ; boltzmann constant in m^2 kg s^-2 K^-1
c = 3e8 ; in m s^-1
q = 1.60217646e-19 ; in Joules
me_csq = m_e*c^2/(1000*q) ; in KeV
R500 = (M500/(4d/3d*!dpi*500d*rho_crit(redshift, H0, OmegaM)))^(1d/3d)
press_to_sz = sigma_T/me_csq;
; Arnaud parameters
P0 = 8.403/(hubble70)^1.5;
c500 = 1.177
alpha = 1.0510
beta = 5.4905
gamma = 0.3081
params = [alpha, beta, gamma, c500, R500]
E = Efact(redshift, OmegaM)
alphap = 0.12 ; this is what Komatsu et al (10) use.
Pnorm = 1.65e-3*E^(8./3.)*(hubble70*M500/3.0e14)^(0.6667 + alphap)*(hubble70^2); % units are keV/cm^3
Pnorm = Pnorm*P0*1.0e6; % keV/m^3
x = dindgen(100d * cutoff*R500) / 100d
y2 = dblarr(n_elements(x),1)
y2b = dblarr(n_elements(x),1)
for i=0,n_elements(x)-1 do begin
  ; do the projection
  R = x[i]
  y = R
  upperlim = sqrt((cutoff*R500)^2 - R*R)
  if (R le cutoff*R500) then begin
    y2[i] = qromb('proj_arnaud_profile', 0.001, upperlim, /double, EPS=1d-6, JMAX=20 , K=5)
  endif else begin
    y2[i] = 0d
  endelse
endfor
x = x*c500/R500
y2 = press_to_sz*y2*Pnorm*2.0*mpc ; I want to chagne this to a 4, from a 2 is this ok?
r_over_r500 = x/c500*R500 ;I think that the R500 should be squared to yeild correct result?
absy_150ghz = y2 ; -1*y, (i.e. -1*dT/T, assuming g_nu = 1)
r_arcmin = r_over_r500/ang_diam_dist(H0, redshift)*180./!dpi*60.
dT_uK = absy_150ghz*1e6*2.73
if stopit then stop
plot, /YLOG, r_arcmin, dT_uK, TITLE = 'Temp Declination as a function of Radial Arcmin',XTITLE = 'Radial(Arcmin)', YTITLE= 'Temp', THICK=4.0
RETURN
END
